USE [SaleDB]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Client].[COMPANY_PERSONAL_UPDATE]
	@ID			UNIQUEIDENTIFIER,
	@COMPANY	UNIQUEIDENTIFIER,
	@OFFICE		UNIQUEIDENTIFIER,
	@SURNAME	NVARCHAR(128),
	@NAME		NVARCHAR(128),
	@PATRON		NVARCHAR(128),
	@POSITION	UNIQUEIDENTIFIER,
	@NOTE		NVARCHAR(MAX),
	@EMAIL		NVARCHAR(256) = NULL,
	@MAILING	BIT = NULL
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		BEGIN TRAN CompanyPersonal

		DECLARE @TBL TABLE (ID UNIQUEIDENTIFIER)

		DECLARE @NEW_ID UNIQUEIDENTIFIER

		INSERT INTO Client.CompanyPersonal(ID_MASTER, ID_COMPANY, ID_OFFICE, SURNAME, NAME, PATRON, ID_POSITION, NOTE, EMAIL, MAILING, BDATE, EDATE, STATUS, UPD_USER)
			OUTPUT inserted.ID INTO @TBL
			SELECT ID, ID_COMPANY, ID_OFFICE, SURNAME, NAME, PATRON, ID_POSITION, NOTE, EMAIL, MAILING, BDATE, EDATE, 2, UPD_USER
			FROM Client.CompanyPersonal
			WHERE ID = @ID

		SELECT @NEW_ID = ID
		FROM @TBL

		INSERT INTO Client.CompanyPersonalPhone(ID_PERSONAL, ID_TYPE, PHONE, PHONE_S, NOTE, STATUS, UPD_DATE, UPD_USER)
			SELECT @NEW_ID, ID_TYPE, PHONE, PHONE_S, NOTE, STATUS, UPD_DATE, UPD_USER
			FROM Client.CompanyPersonalPhone
			WHERE ID_PERSONAL = @ID

		UPDATE Client.CompanyPersonal
		SET ID_OFFICE	=	@OFFICE,
			SURNAME		=	@SURNAME,
			NAME		=	@NAME,
			PATRON		=	@PATRON,
			ID_POSITION	=	@POSITION,
			NOTE		=	@NOTE,
			EMAIL		=	@EMAIL,
			MAILING		=	@MAILING,
			BDATE		=	GETDATE(),
			UPD_USER	=	ORIGINAL_LOGIN()
		WHERE ID = @ID

		EXEC Client.COMPANY_REINDEX @COMPANY, NULL

		COMMIT TRAN CompanyPersonal
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN CompanyPersonal

		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Security.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GRANT EXECUTE ON [Client].[COMPANY_PERSONAL_UPDATE] TO rl_company_personal_w;
GO