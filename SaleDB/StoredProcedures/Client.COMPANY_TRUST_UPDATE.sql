USE [SaleDB]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Client].[COMPANY_TRUST_UPDATE]
	@ID			UNIQUEIDENTIFIER,
	@COMPANY	UNIQUEIDENTIFIER,
	@OFFICE		UNIQUEIDENTIFIER,
	@DATE		SMALLDATETIME,
	@TRUST		BIT,
	@NOTE		NVARCHAR(MAX)
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		BEGIN TRAN CompanyTrust

		INSERT INTO Client.CompanyTrust(ID_MASTER, ID_COMPANY, ID_OFFICE, DATE, TRUST, NOTE, STATUS, BDATE, EDATE, UPD_USER)
			SELECT ID, ID_COMPANY, ID_OFFICE, DATE, TRUST, NOTE, 2, BDATE, EDATE, UPD_USER
			FROM Client.CompanyTrust
			WHERE ID = @ID

		UPDATE Client.CompanyTrust
		SET ID_OFFICE	=	@OFFICE,
			DATE		=	@DATE,
			TRUST		=	@TRUST,
			NOTE		=	@NOTE,
			BDATE		=	GETDATE(),
			UPD_USER	=	ORIGINAL_LOGIN()
		WHERE ID = @ID

		EXEC Client.COMPANY_REINDEX @COMPANY, NULL

		COMMIT TRAN CompanyTrust
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN CompanyTrust

		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Security.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Client].[COMPANY_TRUST_UPDATE] TO rl_trust_w;
GO