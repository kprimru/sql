USE [SaleDB]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Client].[COMPANY_PHONE_UPDATE]
	@ID			UNIQUEIDENTIFIER,
	@COMPANY	UNIQUEIDENTIFIER,
	@OFFICE		UNIQUEIDENTIFIER,
	@TYPE		UNIQUEIDENTIFIER,
	@PHONE		NVARCHAR(128),
	@PHONE_S	NVARCHAR(64),
	@NOTE		NVARCHAR(MAX)
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		BEGIN TRAN CompanyPhone

		INSERT INTO Client.CompanyPhone(ID_MASTER, ID_COMPANY, ID_OFFICE, ID_TYPE, PHONE, PHONE_S, NOTE, BDATE, EDATE, STATUS, UPD_USER)
			SELECT ID, ID_COMPANY, ID_OFFICE, ID_TYPE, PHONE, PHONE_S, NOTE, BDATE, EDATE, 2, UPD_USER
			FROM Client.CompanyPhone
			WHERE ID = @ID

		UPDATE Client.CompanyPhone
		SET ID_OFFICE	= @OFFICE,
			ID_TYPE = @TYPE,
			PHONE = @PHONE,
			PHONE_S = @PHONE_S,
			NOTE = @NOTE,
			BDATE = GETDATE(),
			UPD_USER = ORIGINAL_LOGIN()
		WHERE ID = @ID

		EXEC Client.COMPANY_REINDEX @COMPANY, NULL

		COMMIT TRAN CompanyPhone
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN CompanyPhone

		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Security.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GRANT EXECUTE ON [Client].[COMPANY_PHONE_UPDATE] TO rl_phone_w;
GO