USE [ClientDB]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
IF OBJECT_ID('[Task].[TASK_SAVE]', 'P ') IS NULL EXEC('CREATE PROCEDURE [Task].[TASK_SAVE]  AS SELECT 1')
GO
ALTER PROCEDURE [Task].[TASK_SAVE]
	@ID			UNIQUEIDENTIFIER,
	@DATE		SMALLDATETIME,
	@TIME		DATETIME,
	@RECEIVER	NVARCHAR(128),
	@ID_CLIENT	INT,
	@SHORT		NVARCHAR(256),
	@NOTE		NVARCHAR(MAX),
	@EXPIRE		SMALLDATETIME,
	@NOTIFY		BIT = NULL,
	@NOTIFY_DAY	SMALLINT = NULL
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE
		@DebugError		VarChar(512),
		@DebugContext	Xml,
		@Params			Xml;

	EXEC [Debug].[Execution@Start]
		@Proc_Id		= @@ProcId,
		@Params			= @Params,
		@DebugContext	= @DebugContext OUT

	BEGIN TRY

		IF @ID IS NULL
			INSERT INTO Task.Tasks(DATE, TIME, RECEIVER, ID_CLIENT, ID_STATUS, SHORT, NOTE, EXPIRE, NOTIFY, NOTIFY_DAY)
				SELECT @DATE, @TIME, @RECEIVER, @ID_CLIENT, ID, @SHORT, @NOTE, @EXPIRE, @NOTIFY, @NOTIFY_DAY
				FROM Task.TaskStatus
				WHERE PSEDO = N'ACTIVE'
		ELSE
		BEGIN
			INSERT INTO Task.Tasks(ID_MASTER, DATE, TIME, SENDER, RECEIVER, ID_CLIENT, ID_STATUS, SHORT, NOTE, EXPIRE, EXEC_DATE, EXEC_NOTE, NOTIFY, NOTIFY_DAY, STATUS, UPD_DATE, UPD_USER)
				SELECT @ID, DATE, TIME, SENDER, RECEIVER, ID_CLIENT, ID_STATUS, SHORT, NOTE, EXPIRE, EXEC_DATE, EXEC_NOTE, NOTIFY, NOTIFY_DAY, 2, UPD_DATE, UPD_USER
				FROM Task.Tasks
				WHERE ID = @ID

			UPDATE Task.Tasks
			SET DATE = @DATE,
				TIME = @TIME,
				RECEIVER = @RECEIVER,
				ID_CLIENT = @ID_CLIENT,
				SHORT = @SHORT,
				NOTE = @NOTE,
				EXPIRE = @EXPIRE,
				NOTIFY	=	@NOTIFY,
				NOTIFY_DAY = @NOTIFY_DAY,
				UPD_DATE = GETDATE(),
				UPD_USER = ORIGINAL_LOGIN()
			WHERE ID = @ID
		END

		EXEC [Debug].[Execution@Finish] @DebugContext = @DebugContext, @Error = NULL;
	END TRY
	BEGIN CATCH
		SET @DebugError = Error_Message();

		EXEC [Debug].[Execution@Finish] @DebugContext = @DebugContext, @Error = @DebugError;

		EXEC [Maintenance].[ReRaise Error];
	END CATCH
END
GO
GRANT EXECUTE ON [Task].[TASK_SAVE] TO rl_task_w;
GO
