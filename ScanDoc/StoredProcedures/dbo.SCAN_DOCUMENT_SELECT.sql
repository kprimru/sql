USE [ScanDoc]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
IF OBJECT_ID('[dbo].[SCAN_DOCUMENT_SELECT]', 'P ') IS NULL EXEC('CREATE PROCEDURE [dbo].[SCAN_DOCUMENT_SELECT]  AS SELECT 1')
GO
ALTER PROCEDURE [dbo].[SCAN_DOCUMENT_SELECT]
	@START		SMALLDATETIME,
	@FINISH		SMALLDATETIME,
	@CATEGORY	INT,
	@COMPANY	INT,
	@DIRECTION	INT,
	@NAME		NVARCHAR(256)
AS
BEGIN
	SET NOCOUNT ON;

	SELECT
		ROW_NUMBER() OVER(ORDER BY a.DATE DESC, c.NAME, a.ID DESC) AS RN,
		a.ID, a.DATE, a.NAME, b.NAME AS COMPANY, c.NAME AS DIRECTION, d.FULL_NAME AS CATEGORY, f.NAME AS FORM,
		a.DOC_NUM + ' от ' + CONVERT(NVARCHAR(64), DOC_DATE, 104) AS DOC_NUM_DATE,
		ISNULL((
			SELECT W
			FROM
				dbo.CategoryUsers z
				INNER JOIN dbo.Users y ON z.ID_USER = y.ID
			WHERE y.LGN = ORIGINAL_LOGIN()
				AND z.ID_CATEGORY = a.ID_CATEGORY
		), 0) AS RO,
		a.RES
	FROM
		dbo.ScanDocument a
		INNER JOIN dbo.Company b ON a.ID_COMPANY = b.ID
		INNER JOIN dbo.Direction c ON a.ID_DIRECTION = c.ID
		INNER JOIN dbo.CategoryView d ON a.ID_CATEGORY = d.ID
		LEFT OUTER JOIN dbo.Form f ON a.ID_FORM = f.ID
	WHERE a.STATUS = 1
		AND (a.DATE >= @START OR @START IS NULL)
		AND (a.DATE <= @FINISH OR @FINISH IS NULL)
		AND (a.ID_CATEGORY = @CATEGORY OR @CATEGORY IS NULL)
		AND (a.ID_DIRECTION = @DIRECTION OR @DIRECTION IS NULL)
		AND (a.ID_COMPANY = @COMPANY OR @COMPANY IS NULL)
		AND (a.NAME LIKE @NAME OR @NAME IS NULL)
	ORDER BY a.DATE DESC, c.NAME, a.ID DESC
END
GO
GRANT EXECUTE ON [dbo].[SCAN_DOCUMENT_SELECT] TO rl_reader;
GRANT EXECUTE ON [dbo].[SCAN_DOCUMENT_SELECT] TO rl_user;
GO
