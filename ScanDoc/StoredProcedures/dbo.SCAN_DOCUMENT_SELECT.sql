USE [ScanDoc]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
IF OBJECT_ID('[dbo].[SCAN_DOCUMENT_SELECT]', 'P ') IS NULL EXEC('CREATE PROCEDURE [dbo].[SCAN_DOCUMENT_SELECT]  AS SELECT 1')
GO
ALTER PROCEDURE [dbo].[SCAN_DOCUMENT_SELECT]
	@START		SMALLDATETIME,
	@FINISH		SMALLDATETIME,
	@CATEGORY	INT,
	@COMPANY	INT,
	@DIRECTION	INT,
	@NAME		NVARCHAR(256)
AS
BEGIN
	SET NOCOUNT ON;

	SELECT
		ROW_NUMBER() OVER(ORDER BY SD.DATE DESC, D.NAME, SD.ID DESC) AS RN,
		SD.ID, SD.DATE, SD.NAME, C.NAME AS COMPANY, D.NAME AS DIRECTION, CT.FULL_NAME AS CATEGORY, F.NAME AS FORM,
		SD.DOC_NUM + ' от ' + CONVERT(NVARCHAR(64), SD.DOC_DATE, 104) AS DOC_NUM_DATE,
		ISNULL((
			SELECT W
			FROM
				dbo.CategoryUsers		AS CU
				INNER JOIN dbo.Users	AS U ON CU.ID_USER = U.ID
			WHERE U.LGN = ORIGINAL_LOGIN()
				AND CU.ID_CATEGORY = SD.ID_CATEGORY
		), 0) AS RO,
		SD.RES, P.[PagesCount]
	FROM dbo.ScanDocument			AS SD
	INNER JOIN dbo.Company			AS C ON SD.ID_COMPANY = C.ID
	INNER JOIN dbo.Direction		AS D ON SD.ID_DIRECTION = D.ID
	INNER JOIN dbo.CategoryView		AS CT ON SD.ID_CATEGORY = CT.ID
	LEFT JOIN dbo.Form				AS F ON SD.ID_FORM = F.ID
	OUTER APPLY
	(
		SELECT [PagesCount] = COUNT(*)
		FROM dbo.ScanPages AS P
		WHERE P.ID_DOCUMENT = SD.ID
	) AS P
	WHERE SD.STATUS = 1
		AND (SD.DATE >= @START OR @START IS NULL)
		AND (SD.DATE <= @FINISH OR @FINISH IS NULL)
		AND (SD.ID_CATEGORY = @CATEGORY OR @CATEGORY IS NULL)
		AND (SD.ID_DIRECTION = @DIRECTION OR @DIRECTION IS NULL)
		AND (SD.ID_COMPANY = @COMPANY OR @COMPANY IS NULL)
		AND (SD.NAME LIKE @NAME OR @NAME IS NULL)
	ORDER BY SD.DATE DESC, D.NAME, SD.ID DESC
END
GO
GRANT EXECUTE ON [dbo].[SCAN_DOCUMENT_SELECT] TO rl_reader;
GRANT EXECUTE ON [dbo].[SCAN_DOCUMENT_SELECT] TO rl_user;
GO
