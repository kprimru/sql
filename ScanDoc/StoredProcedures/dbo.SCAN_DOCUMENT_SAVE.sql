USE [ScanDoc]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
IF OBJECT_ID('[dbo].[SCAN_DOCUMENT_SAVE]', 'P ') IS NULL EXEC('CREATE PROCEDURE [dbo].[SCAN_DOCUMENT_SAVE]  AS SELECT 1')
GO
ALTER PROCEDURE [dbo].[SCAN_DOCUMENT_SAVE]
	@ID			INT	OUTPUT,
	@CATEGORY	INT,
	@DIRECTION	INT,
	@COMPANY	INT,
	@FORM		INT,
	@NAME		NVARCHAR(512),
	@DATE		SMALLDATETIME,
	@DOC_NUM	NVARCHAR(128),
	@DOC_DATE	SMALLDATETIME
AS
BEGIN
	SET NOCOUNT ON;

	IF @ID IS NULL
	BEGIN
		INSERT INTO dbo.ScanDocument(ID_CATEGORY, ID_DIRECTION, ID_COMPANY, ID_FORM, NAME, DATE, DOC_NUM, DOC_DATE)
			VALUES(@CATEGORY, @DIRECTION, @COMPANY, @FORM, @NAME, @DATE, @DOC_NUM, @DOC_DATE)

		SELECT @ID = SCOPE_IDENTITY()
	END
	ELSE
	BEGIN
		DECLARE @NEWID INT

		INSERT INTO dbo.ScanDocument(ID_MASTER, ID_CATEGORY, ID_DIRECTION, ID_COMPANY, ID_FORM, NAME, DATE, DOC_NUM, DOC_DATE, STATUS, UPD_DATE, UPD_USER)
			SELECT @ID, ID_CATEGORY, ID_DIRECTION, ID_COMPANY, ID_FORM, NAME, DATE, DOC_NUM, DOC_DATE, 2, UPD_DATE, UPD_USER
			FROM dbo.ScanDocument
			WHERE ID = @ID

		SELECT @NEWID = SCOPE_IDENTITY()

		UPDATE dbo.ScanPages
		SET ID_DOCUMENT = @NEWID
		WHERE ID_DOCUMENT = @ID

		UPDATE dbo.ScanDocument
		SET ID_CATEGORY		=	@CATEGORY,
			ID_DIRECTION	=	@DIRECTION,
			ID_COMPANY		=	@COMPANY,
			ID_FORM			=	@FORM,
			NAME			=	@NAME,
			DATE			=	@DATE,
			DOC_NUM			=	@DOC_NUM,
			DOC_DATE		=	@DOC_DATE,
			UPD_DATE		=	GETDATE(),
			UPD_USER		=	ORIGINAL_LOGIN(),
			ExportDateTime	=	NULL
		WHERE ID = @ID
	END
END
GO
GRANT EXECUTE ON [dbo].[SCAN_DOCUMENT_SAVE] TO rl_admin;
GO
