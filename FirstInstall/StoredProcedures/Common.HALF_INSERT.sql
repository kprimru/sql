USE [FirstInstall]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Common].[HALF_INSERT]
	@HLF_NAME		VARCHAR(50),
	@HLF_BEGIN_DATE	SMALLDATETIME,
	@HLF_END_DATE	SMALLDATETIME,
	@HLF_DATE		SMALLDATETIME,
	@HLF_ID			UNIQUEIDENTIFIER = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON

	DECLARE @OLD	VARCHAR(MAX)
	DECLARE @NEW	VARCHAR(MAX)

	EXEC Common.PROTOCOL_VALUE_GET 'HALF', NULL, @OLD OUTPUT

	DECLARE @TBL TABLE (ID UNIQUEIDENTIFIER)

	DECLARE @MASTERID UNIQUEIDENTIFIER

	INSERT INTO Common.Half(HLFMS_ID)
	OUTPUT INSERTED.HLFMS_ID INTO @TBL
	DEFAULT VALUES


	SELECT	@MASTERID = ID
	FROM	@TBL

	DELETE
	FROM	@TBL


	INSERT INTO
			Common.HalfDetail(
				HLF_NAME,
				HLF_BEGIN_DATE,
				HLF_END_DATE,
				HLF_DATE,
				HLF_ID_MASTER
			)
	OUTPUT INSERTED.HLF_ID INTO @TBL(ID)
	VALUES	(
				@HLF_NAME,
				@HLF_BEGIN_DATE,
				@HLF_END_DATE,
				@HLF_DATE,
				@MASTERID
			)

	SELECT	@HLF_ID = ID
	FROM	@TBL

	EXEC Common.PROTOCOL_VALUE_GET 'HALF', @MASTERID, @NEW OUTPUT

	EXEC Common.PROTOCOL_INSERT 'HALF', 'Новая запись', @MASTERID, @OLD, @NEW

END

GO
GRANT EXECUTE ON [Common].[HALF_INSERT] TO rl_half_i;
GO