USE [FirstInstall]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Claim].[CLAIM_CREATE]
	@IND_ID	VARCHAR(MAX),
	@CLM_ID	UNIQUEIDENTIFIER = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @USER UNIQUEIDENTIFIER

	SELECT @USER = US_ID_MASTER
	FROM Security.UserActive
	WHERE US_LOGIN = ORIGINAL_LOGIN()

	DECLARE @NUM INT

	SELECT @NUM = MAX(CLM_NUM) + 1
	FROM Claim.Claims
	WHERE CONVERT(VARCHAR(8), CLM_DATE, 112) = CONVERT(VARCHAR(8), GETDATE(), 112)

	IF @NUM IS NULL
		SET @NUM = 1

	DECLARE @TBL TABLE(ID UNIQUEIDENTIFIER)

	INSERT INTO Claim.Claims(CLM_ID_USER, CLM_NUM)
	OUTPUT INSERTED.CLM_ID INTO @TBL
	VALUES(@USER, @NUM)

	SELECT @CLM_ID = ID
	FROM @TBL

	INSERT INTO Claim.ClaimDetail(
		CLD_ID_CLAIM, CLD_ID_CLIENT, CLD_ID_VENDOR,
		CLD_ID_SYSTEM, CLD_ID_TYPE,
		CLD_ID_NET, CLD_ID_TECH,
		CLD_COUNT, CLD_COMMENT)
		SELECT
			@CLM_ID, CL_ID_MASTER, VD_ID_MASTER, SYS_ID_MASTER,
			DT_ID_MASTER, NT_ID_MASTER, TT_ID_MASTER, COUNT(IND_ID), ID_COMMENT
		FROM
			Install.InstallFullView INNER JOIN
			Common.TableFromList(@IND_ID, ',') ON ID = IND_ID
		GROUP BY CL_ID_MASTER, VD_ID_MASTER, SYS_ID_MASTER, DT_ID_MASTER, NT_ID_MASTER, TT_ID_MASTER, ID_COMMENT

	UPDATE Install.InstallDetail
	SET IND_ID_CLAIM = @CLM_ID
	WHERE IND_ID IN
		(
			SELECT ID
			FROM Common.TableFromList(@IND_ID, ',')
		)
END
GO
GRANT EXECUTE ON [Claim].[CLAIM_CREATE] TO rl_claim_w;
GO