USE [FirstInstall]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [Income].[INCOME_FULL_SELECT]
	@BDATE		SMALLDATETIME	= NULL,
	@EDATE		SMALLDATETIME	= NULL,
	@CLIENT		VARCHAR(50)		= NULL,
	@UNPAY		BIT				= NULL,
	@PERS		BIT				= NULL,	
	@RC			INT				= NULL OUTPUT,
	@PERSONAL	UNIQUEIDENTIFIER	= NULL,
	@COLOR		INT				= NULL,
	@HIDE_REPAY	BIT				= NULL,
	@TYPE		UNIQUEIDENTIFIER = NULL,
	@SYSTEM		UNIQUEIDENTIFIER = NULL,
	@NET		UNIQUEIDENTIFIER = NULL,
	@COMMENT	VARCHAR(100) = NULL
WITH EXECUTE AS OWNER
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @SQL NVARCHAR(MAX)

	SET @SQL = 
	'
	SELECT 
		IN_ID, IN_DATE, IN_PAY, 
		CL_ID_MASTER, CL_NAME, 
		VD_ID_MASTER, VD_NAME, 
		ID_ID, ID_NOTE,
		SYS_ID_MASTER, SYS_SHORT, 
		DT_ID_MASTER, DT_NAME, 
		NT_ID_MASTER, --NT_NAME, 
		TT_ID_MASTER, --TT_NAME, 
		NT_NEW_NAME, CONVERT(BIT, 1) AS ID_PAYED,
		ID_COUNT, ID_CALC, ID_PREPAY,
		ID_DEL_SUM, ID_DEL_PRICE, ID_DEL_DISCOUNT, 
		ID_COMMENT,		
		ID_FULL_DATE, ID_REPAYED, ID_REPAY,
		ID_MON_STR,--PR_FIRST_NAME,
		PR_FULL_NAME,
		ID_MON_CNT, ID_SUP_PRICE, ID_SUP_DISCOUNT, 
		ID_SUP_MONTH, ID_PERSONAL,		
		ID_SUP_CONTRACT, ID_SUP_DATE, 
		ID_LOCK, ID_RESTORE, ID_MAIN, ID_COLOR '


	SET @SQL = @SQL + '
	FROM Income.IncomeFullView
	WHERE 1 = 1 '

	IF @BDATE IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND IN_DATE >= @BDATE '
	END

	IF @EDATE IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND IN_DATE <= @EDATE '
	END

	IF @CLIENT IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND CL_NAME LIKE @CLIENT '
	END

	IF @UNPAY IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND ID_FULL_DATE IS NULL '
	END
	
	IF @PERS IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND ISNULL(ID_REPAY, 0) = 0 AND ID_PERSONAL IS NULL '
	END

	IF @COLOR IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND ID_COLOR = @COLOR '
	END

	IF @HIDE_REPAY IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND ID_REPAYED = 0 '
	END

	IF @PERSONAL IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND EXISTS(SELECT * FROM Income.IncomePersonal WHERE IP_ID_INCOME = ID_ID AND IP_ID_PERSONAL = @PERSONAL) '
	END

	IF @TYPE IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND DT_ID_MASTER = @TYPE '
	END

	IF @SYSTEM IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND SYS_ID_MASTER = @SYSTEM '
	END

	IF @NET IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND (NT_ID_MASTER = @NET OR TT_ID_MASTER = @NET) '
	END

	IF @COMMENT IS NOT NULL
	BEGIN
		SET @SQL = @SQL + ' AND ID_NOTE LIKE @COMMENT AND ID_NOTE <> '''''
	END	

	SET @SQL = @SQL + '
	ORDER BY IN_DATE DESC, CL_NAME, SYS_ORDER'

	EXEC sp_executesql	@SQL, 
						N'@BDATE SMALLDATETIME, @EDATE SMALLDATETIME, @CLIENT VARCHAR(50), @PERSONAL UNIQUEIDENTIFIER, @COLOR INT, @TYPE UNIQUEIDENTIFIER, @SYSTEM UNIQUEIDENTIFIER, @NET UNIQUEIDENTIFIER, @COMMENT VARCHAR(100)',
						@BDATE, @EDATE, @CLIENT, @PERSONAL, @COLOR, @TYPE, @SYSTEM, @NET, @COMMENT

	SELECT @RC = @@ROWCOUNT
END


