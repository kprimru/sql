USE [FirstInstall]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Distr].[DISTR_IMPORT_EXCHANGE_SELECT]
	@BEGIN	SMALLDATETIME,
	@END	SMALLDATETIME
AS
BEGIN
	SET NOCOUNT ON;

	SELECT
		CONVERT(BIT, 1) AS CHECKED,
		DF_ID, DT_SHORT, DT_ID_MASTER, DF_DISTR, DF_COMP,
		SYS_STR, SYS_ID, OLD_SYS, NEW_SYS,
		NET_STR, NET_ID, OLD_NET, NEW_NET,
		DF_CREATE, COMMENT, SH_NAME, SH_ID
	FROM
		(
			SELECT
				DF_ID, b.DT_ID_MASTER, DT_SHORT, DF_DISTR, DF_COMP, SystemOrder,
				CASE
					WHEN c.SYS_ID_MASTER = d.SYS_ID_MASTER THEN c.SYS_SHORT
					ELSE 'с ' + c.SYS_SHORT + ' на ' + d.SYS_SHORT
				END AS SYS_STR,
				CASE
					WHEN c.SYS_ID_MASTER = d.SYS_ID_MASTER THEN c.SYS_ID_MASTER
					ELSE NULL
				END AS SYS_ID,
				CASE
					WHEN c.SYS_ID_MASTER = d.SYS_ID_MASTER THEN NULL
					ELSE c.SYS_ID_MASTER
				END AS OLD_SYS,
				CASE
					WHEN c.SYS_ID_MASTER = d.SYS_ID_MASTER THEN NULL
					ELSE d.SYS_ID_MASTER
				END AS NEW_SYS,
				CASE
					WHEN e.ID = f.ID THEN e.SHORT
					ELSE 'с ' + e.SHORT + ' на ' + f.SHORT
				END AS NET_STR,
				CASE
					WHEN e.ID = f.ID THEN e.ID
					ELSE NULL
				END AS NET_ID,
				CASE
					WHEN e.ID = f.ID THEN NULL
					ELSE e.ID
				END AS OLD_NET,
				CASE
					WHEN e.ID = f.ID THEN NULL
					ELSE f.ID
				END AS NEW_NET,
				DF_CREATE, COMMENT, g.NAME AS SH_NAME, g.ID AS SH_ID
			FROM
				[PC275-SQL\ALPHA].ClientDB.Din.DistrExchangeView a
				LEFT OUTER JOIN Distr.DistrTypeActive b ON a.SST_REG = b.DT_REG
				LEFT OUTER JOIN Distr.SystemActive c ON c.SYS_REG = a.OLD_SYSTEM
				LEFT OUTER JOIN Distr.SystemActive d ON d.SYS_REG = a.NEW_SYSTEM
				LEFT OUTER JOIN Distr.NetAll e ON e.NET_COUNT = a.OLD_NET AND e.TECH = a.OLD_TECH
				LEFT OUTER JOIN Distr.NetAll f ON f.NET_COUNT = a.NEW_NET AND f.TECH = a.NEW_TECH
				LEFT OUTER JOIN Distr.Subhost g ON g.REG = a.SUBHOST_REG
			WHERE DF_RIC = 20
				AND (DF_CREATE >= @BEGIN OR @BEGIN IS NULL)
				AND (DF_CREATE <= @END OR @END IS NULL)
		) AS t
	WHERE NOT EXISTS
			(
				SELECT *
				FROM Distr.DistrIncome z
				WHERE z.ID_TYPE = t.DT_ID_MASTER
					AND z.NUM = t.DF_DISTR
					AND z.COMP = t.DF_COMP
					AND (z.ID_SYSTEM = t.SYS_ID OR z.ID_SYSTEM IS NULL AND t.SYS_ID IS NULL)
					AND (z.ID_OLD_SYS = t.OLD_SYS OR z.ID_OLD_SYS IS NULL AND t.OLD_SYS IS NULL)
					AND (z.ID_NEW_SYS = t.NEW_SYS OR z.ID_NEW_SYS IS NULL AND t.NEW_SYS IS NULL)
					AND (z.ID_NET = t.NET_ID OR z.ID_NET IS NULL AND t.NET_ID IS NULL)
					AND (z.ID_OLD_NET = t.OLD_NET OR z.ID_OLD_NET IS NULL AND t.OLD_NET IS NULL)
					AND (z.ID_NEW_NET = t.NEW_NET OR z.ID_NEW_NET IS NULL AND t.NEW_NET IS NULL)
					AND z.INCOME_DATE = t.DF_CREATE
			)
	ORDER BY DF_CREATE DESC, SystemOrder, DF_DISTR
END
GO
GRANT EXECUTE ON [Distr].[DISTR_IMPORT_EXCHANGE_SELECT] TO rl_distr_income_w;
GO
