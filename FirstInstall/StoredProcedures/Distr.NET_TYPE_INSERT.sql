USE [FirstInstall]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Distr].[NET_TYPE_INSERT]
	@NT_NAME	VARCHAR(50),
	@NT_SHORT	VARCHAR(50),
	@NT_FULL	VARCHAR(50),
	@NT_COEF	DECIMAL(8, 4),
	@NT_DATE	SMALLDATETIME,
	@NT_ID		UNIQUEIDENTIFIER = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON

	DECLARE @OLD	VARCHAR(MAX)
	DECLARE @NEW	VARCHAR(MAX)

	EXEC Common.PROTOCOL_VALUE_GET 'NET_TYPE', NULL, @OLD OUTPUT

	DECLARE @TBL TABLE (ID UNIQUEIDENTIFIER)

	DECLARE @MASTERID UNIQUEIDENTIFIER

	INSERT INTO Distr.NetType(NTMS_ID)
	OUTPUT INSERTED.NTMS_ID INTO @TBL
	DEFAULT VALUES


	SELECT	@MASTERID = ID
	FROM	@TBL

	DELETE
	FROM	@TBL


	INSERT INTO
			Distr.NetTypeDetail(
				NT_NAME,
				NT_SHORT,
				NT_FULL,
				NT_COEF,
				NT_DATE,
				NT_ID_MASTER
			)
	OUTPUT INSERTED.NT_ID INTO @TBL(ID)
	VALUES	(
				@NT_NAME,
				@NT_SHORT,
				@NT_FULL,
				@NT_COEF,
				@NT_DATE,
				@MASTERID
			)

	SELECT	@NT_ID = ID
	FROM	@TBL

	EXEC Common.PROTOCOL_VALUE_GET 'NET_TYPE', @MASTERID, @NEW OUTPUT

	EXEC Common.PROTOCOL_INSERT 'NET_TYPE', 'Новая запись', @MASTERID, @OLD, @NEW

END

GO
GRANT EXECUTE ON [Distr].[NET_TYPE_INSERT] TO rl_net_type_i;
GO