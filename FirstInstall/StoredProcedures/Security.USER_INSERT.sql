USE [FirstInstall]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Security].[USER_INSERT]
	@US_NAME	VARCHAR(50),
	@US_LOGIN	VARCHAR(50),
	@US_NOTE	VARCHAR(250),
	@US_DATE	SMALLDATETIME,
	@US_ID		UNIQUEIDENTIFIER = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON

	DECLARE @TBL TABLE (ID UNIQUEIDENTIFIER)

	DECLARE @MASTERID UNIQUEIDENTIFIER

	INSERT INTO Security.Users(USMS_ID)
	OUTPUT INSERTED.USMS_ID INTO @TBL
	DEFAULT VALUES


	SELECT	@MASTERID = ID
	FROM	@TBL

	DELETE
	FROM	@TBL


	IF NOT EXISTS
		(
			SELECT *
			FROM sys.server_principals
			WHERE [name] = @US_LOGIN
		)
	BEGIN
		IF CHARINDEX('\', @US_LOGIN) <> 0
			EXEC ('CREATE LOGIN [' + @US_LOGIN + '] FROM WINDOWS')
		ELSE
			EXEC ('CREATE LOGIN [' + @US_LOGIN + '] WITH CHECK_POLICY = OFF')
	END

	IF NOT EXISTS
		(
			SELECT *
			FROM sys.database_principals
			WHERE [name] = @US_LOGIN
		)
	BEGIN
		EXEC ('CREATE USER [' + @US_LOGIN + '] FOR LOGIN [' + @US_LOGIN + ']')
	END

	INSERT INTO
			Security.UserDetail(
				US_NAME,
				US_LOGIN,
				US_NOTE,
				US_DATE,
				US_ID_MASTER
			)
	OUTPUT INSERTED.US_ID INTO @TBL(ID)
	VALUES	(
				@US_NAME,
				@US_LOGIN,
				@US_NOTE,
				@US_DATE,
				@MASTERID
			)

	SELECT	@US_ID = ID
	FROM	@TBL
END

GO
GRANT EXECUTE ON [Security].[USER_INSERT] TO rl_user_i;
GO
