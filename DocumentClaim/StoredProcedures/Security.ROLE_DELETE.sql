USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Security].[ROLE_DELETE]
	@ID		UNIQUEIDENTIFIER
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		IF EXISTS
			(
				SELECT *
				FROM Security.Roles
				WHERE ID_MASTER = @ID
			)
		BEGIN
			DECLARE @SUB	UNIQUEIDENTIFIER
			DECLARE RL CURSOR LOCAL FOR
				SELECT ID FROM Security.Roles WHERE ID_MASTER = @ID

			OPEN RL

			FETCH NEXT FROM RL INTO @SUB

			WHILE @@FETCH_STATUS = 0
			BEGIN
				EXEC Security.ROLE_DELETE @SUB

				FETCH NEXT FROM RL INTO @SUB
			END

			CLOSE RL
			DEALLOCATE RL
		END

		DECLARE @NAME NVARCHAR(128)

		SELECT @NAME = NAME
		FROM Security.Roles
		WHERE ID = @ID

		DELETE FROM Security.Roles
		WHERE ID = @ID

		IF ISNULL(@NAME, N'') <> N''
			EXEC ('DROP ROLE [' + @NAME + ']')

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Security].[ROLE_DELETE] TO rl_role_d;
GO
