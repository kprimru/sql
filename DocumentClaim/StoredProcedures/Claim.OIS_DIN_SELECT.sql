USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Claim].[OIS_DIN_SELECT]
	@ID	INT
WITH EXECUTE AS OWNER
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		IF OBJECT_ID('tempdb..#din_temp') IS NOT NULL
			DROP TABLE #din_temp

		CREATE TABLE #din_temp
			(
				ID			INT,
				ID_MASTER	INT,
				DF_ID		INT,
				NT_ID		INT,
				SST_ID		INT,
				ID_SYSTEM	INT,
				DISTR		INT,
				COMP		TINYINT,
				DF_SELECT	BIT,
				DIS_STR		NVARCHAR(128),
				NT_NAME		NVARCHAR(128),
				SST_NAME	NVARCHAR(128),
				DF_CREATE	DATETIME,
				DIS_STATUS	INT,
				DS_INDEX	INT
			)

		INSERT INTO #din_temp
			EXEC [PC275-SQL\ALPHA].ClientDB.Din.CLIENT_DIN_SELECT @ID

		DELETE
		FROM #din_temp
		WHERE ID_MASTER IS NOT NULL

		SELECT
			DF_SELECT AS CHECKED, DISTR, COMP,
			CONVERT(NVARCHAR(32), DISTR) +
				CASE COMP
					WHEN 1 THEN N''
					ELSE N'/' + CONVERT(NVARCHAR(8), COMP)
				END AS DIS_STR,
			d.ID AS ID_SYSTEM, e.ID AS ID_NET, e.SHORT AS NET, d.SHORT AS SYS_SHORT
		FROM
			#din_temp a
			INNER JOIN [PC275-SQL\ALPHA].ClientDB.dbo.SystemTable b ON a.ID_SYSTEM = b.SystemID
			INNER JOIN [PC275-SQL\ALPHA].ClientDB.Din.NetType c ON a.NT_ID = c.NT_ID
			INNER JOIN Distr.System d ON d.REG = b.SystemBaseName
			INNER JOIN Distr.Net e ON c.NT_NET = e.NET_COUNT AND c.NT_TECH = e.TECH
		ORDER BY d.ORD, e.COEF, TECH, NET_COUNT, DISTR, COMP

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Claim].[OIS_DIN_SELECT] TO rl_claim_distr;
GO
