USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Security].[PERMISSION_GROUP_CHECK]
	@ROLE	UNIQUEIDENTIFIER,
	@CNT	INT = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		;WITH roles AS
			(
				SELECT ID, ID_MASTER, NAME
				FROM Security.Roles r
				WHERE ID = @ROLE

				UNION ALL

				SELECT t.ID, t.ID_MASTER, t.NAME
				FROM
					roles r
					INNER JOIN Security.Roles t ON r.ID = t.ID_MASTER
			)
			SELECT @CNT = COUNT(*)
			FROM
				roles a
				--INNER JOIN Security.RoleCaptionView b ON a.ID = b.ID
				INNER JOIN Security.Roles b ON a.ID = b.ID
			WHERE b.NAME IS NOT NULL AND b.NAME <> ''

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Security].[PERMISSION_GROUP_CHECK] TO rl_user_permission_u;
GO
