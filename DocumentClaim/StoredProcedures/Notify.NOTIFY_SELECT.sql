USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Notify].[NOTIFY_SELECT]
	@SENDER		NVARCHAR(MAX),
	@RECEIVER	NVARCHAR(MAX),
	@MODULE		NVARCHAR(MAX),
	@TXT		NVARCHAR(512),
	@BEGIN		DATETIME,
	@END		DATETIME,
	@RC			INT = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		IF OBJECT_ID('tempdb..#sender') IS NOT NULL
			DROP TABLE #sender

		IF OBJECT_ID('tempdb..#receiver') IS NOT NULL
			DROP TABLE #receiver

		IF OBJECT_ID('tempdb..#module') IS NOT NULL
			DROP TABLE #module

		CREATE TABLE #sender
			(
				ID UNIQUEIDENTIFIER PRIMARY KEY
			)

		CREATE TABLE #receiver
			(
				ID UNIQUEIDENTIFIER PRIMARY KEY
			)

		CREATE TABLE #module
			(
				ID NVARCHAR(128) PRIMARY KEY
			)

		IF @SENDER IS NULL
			INSERT INTO #sender(ID)
				SELECT DISTINCT ID_SENDER
				FROM Notify.Message
		ELSE
			INSERT INTO #sender(ID)
				SELECT ID
				FROM Common.TableGUIDFromXML(@SENDER)

		IF @RECEIVER IS NULL
			INSERT INTO #receiver(ID)
				SELECT DISTINCT ID_RECEIVER
				FROM Notify.Message
		ELSE
			INSERT INTO #receiver(ID)
				SELECT ID
				FROM Common.TableGUIDFromXML(@RECEIVER)

		IF @MODULE IS NULL
			INSERT INTO #module(ID)
				SELECT DISTINCT MODULE
				FROM Notify.Message
		ELSE
			INSERT INTO #module(ID)
				SELECT ID
				FROM Common.TableStringFromXML(@MODULE)

		SELECT
			a.ID, TXT, e.CAPTION AS SENDER, f.CAPTION AS RECEIVER, ISNULL(g.CAPTION, d.ID) AS MODULE,
			d.ID AS MODULE_NAME, ID_EVENT, DATE, RECEIVE_DATE
		FROM
			Notify.Message a
			INNER JOIN #sender b ON b.ID = a.ID_SENDER
			INNER JOIN #receiver c ON c.ID = a.ID_RECEIVER
			INNER JOIN #module d ON d.ID = a.MODULE
			INNER JOIN Security.Users e ON a.ID_SENDER = e.ID
			INNER JOIN Security.Users f ON a.ID_RECEIVER = f.ID
			LEFT OUTER JOIN Common.ModuleView g ON g.NAME = a.MODULE
		WHERE (DATE >= @BEGIN OR @BEGIN IS NULL)
			AND (DATE <= @END OR @END IS NULL)
			AND (TXT LIKE @TXT OR @TXT IS NULL)
		ORDER BY DATE DESC, e.NAME, f.NAME

		SELECT @RC = @@ROWCOUNT

		IF OBJECT_ID('tempdb..#sender') IS NOT NULL
			DROP TABLE #sender

		IF OBJECT_ID('tempdb..#receiver') IS NOT NULL
			DROP TABLE #receiver

		IF OBJECT_ID('tempdb..#module') IS NOT NULL
			DROP TABLE #module

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Notify].[NOTIFY_SELECT] TO rl_notify_journal;
GRANT EXECUTE ON [Notify].[NOTIFY_SELECT] TO rl_notify_journal_all;
GO
