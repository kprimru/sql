USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Distr].[DIN_CHECK]
	@ID_SYSTEM	UNIQUEIDENTIFIER,
	@ID_NET		UNIQUEIDENTIFIER,
	@DISTR		INT,
	@COMP		TINYINT,
	@RES		INT = NULL OUTPUT
WITH EXECUTE AS OWNER
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		SET @RES = 0

		IF EXISTS
			(
				SELECT *
				FROM
					(
						SELECT REG, TECH, NET_COUNT
						FROM
							Distr.System sys
							CROSS JOIN Distr.Net nt
						WHERE sys.ID = @ID_SYSTEM AND nt.ID = @ID_NET
					) AS a
					INNER JOIN [PC275-SQL\ALPHA].ClientDB.dbo.SystemTable b ON a.REG = b.SystemBaseName
					INNER JOIN [PC275-SQL\ALPHA].ClientDB.Din.NetType c ON a.TECH = c.NT_TECH AND a.NET_COUNT = c.NT_NET
					INNER JOIN [PC275-SQL\ALPHA].ClientDB.Din.DinFiles d ON c.NT_ID = d.DF_ID_NET AND b.SystemID = d.DF_ID_SYS AND d.DF_DISTR = @DISTR AND d.DF_COMP = @COMP
			)
			SET @RES = 1

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Distr].[DIN_CHECK] TO rl_claim_distr;
GO
