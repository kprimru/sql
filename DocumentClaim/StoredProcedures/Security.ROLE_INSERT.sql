USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Security].[ROLE_INSERT]
	@PARENT		UNIQUEIDENTIFIER,
	@NAME		NVARCHAR(128),
	@CAPTION	NVARCHAR(256),
	@NOTE		NVARCHAR(MAX),
	@ID			UNIQUEIDENTIFIER = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @TXT NVARCHAR(3000)

	IF EXISTS
		(
			SELECT * FROM sys.database_principals WHERE name = @NAME
		)
	BEGIN
		SET @TXT = N'Роль или пользователь "' + ISNULL(@NAME, '') + N'" уже существует. Выберите другое название.'

		RAISERROR(@TXT, 16, 1)

		RETURN
	END

	DECLARE @TBL TABLE (ID UNIQUEIDENTIFIER)

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		INSERT INTO Security.Roles(ID_MASTER, NAME, CAPTION, NOTE)
			OUTPUT inserted.ID INTO @TBL
			VALUES(@PARENT, @NAME, @CAPTION, @NOTE)

		SELECT @ID = ID FROM @TBL

		IF ISNULL(@NAME, N'') <> N''
			EXEC ('CREATE ROLE [' + @NAME + ']')

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Security].[ROLE_INSERT] TO rl_role_u;
GO
