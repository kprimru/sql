USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
IF OBJECT_ID('[Security].[DEPARTMENT_INSERT]', 'P ') IS NULL EXEC('CREATE PROCEDURE [Security].[DEPARTMENT_INSERT]  AS SELECT 1')
GO
ALTER PROCEDURE [Security].[DEPARTMENT_INSERT]
	@ID		UNIQUEIDENTIFIER OUTPUT,
	@NAME	NVARCHAR(128)
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		DECLARE @TBL TABLE(ID UNIQUEIDENTIFIER)

		INSERT INTO Security.Department(NAME)
			OUTPUT inserted.ID INTO @TBL
			VALUES(@NAME)

		SELECT @ID = ID
		FROM @TBL

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Security].[DEPARTMENT_INSERT] TO rl_department_u;
GO
