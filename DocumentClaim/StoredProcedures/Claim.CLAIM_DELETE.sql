USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Claim].[CLAIM_DELETE]
	@ID	UNIQUEIDENTIFIER
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		BEGIN TRAN

		DECLARE @TBL TABLE(ID UNIQUEIDENTIFIER)

		DECLARE @OLD_ID	UNIQUEIDENTIFIER

		INSERT INTO Claim.Document(ID_MASTER, DATE, ID_AUTHOR, ID_CLIENT, CL_TYPE, CL_NAME, ID_TYPE, ID_VENDOR, NOTE, STATUS, UPD_DATE, UPD_USER)
			OUTPUT inserted.ID INTO @TBL
			SELECT ID, DATE, ID_AUTHOR, ID_CLIENT, CL_TYPE, CL_NAME, ID_TYPE, ID_VENDOR, NOTE, 2, UPD_DATE, UPD_USER
			FROM Claim.Document
			WHERE ID = @ID

		SELECT @OLD_ID = ID
		FROM @TBL

		UPDATE Claim.Document
		SET STATUS		=	3,
			UPD_DATE	=	GETDATE(),
			UPD_USER	=	ORIGINAL_LOGIN()
		WHERE ID = @ID

		INSERT INTO Claim.DocumentDetail(ID_DOCUMENT, ID_SYSTEM, ID_NEW_SYSTEM, ID_NET, ID_NEW_NET, ID_ACTION, CNT, DISCOUNT)
			SELECT
				@OLD_ID, ID_SYSTEM, ID_NEW_SYSTEM, ID_NET, ID_NEW_NET, ID_ACTION, CNT, DISCOUNT
			FROM Claim.DocumentDetail
			WHERE ID_DOCUMENT = @ID

		INSERT INTO Claim.DocumentDistr(ID_DOCUMENT, ID_SYSTEM, ID_NET, DISTR, COMP)
			SELECT
				@OLD_ID, ID_SYSTEM, ID_NET, DISTR, COMP
			FROM Claim.DocumentDistr
			WHERE ID_DOCUMENT = @ID

		INSERT INTO Claim.DocumentType(ID_DOCUMENT, ID_TYPE)
			SELECT
				@OLD_ID, ID_TYPE
			FROM Claim.DocumentType
			WHERE ID_DOCUMENT = @ID

		INSERT INTO Claim.DocumentService(ID_DOCUMENT, ID_SERVICE, CNT)
			SELECT
				@OLD_ID, ID_SERVICE, CNT
			FROM Claim.DocumentService
			WHERE ID_DOCUMENT = @ID

		COMMIT

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		ROLLBACK

		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Claim].[CLAIM_DELETE] TO rl_claim_d;
GO
