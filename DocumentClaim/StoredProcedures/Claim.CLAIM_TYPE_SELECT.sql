USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Claim].[CLAIM_TYPE_SELECT]
	@ID		UNIQUEIDENTIFIER,
	@ALL	BIT = 0,
	@RC		INT = NULL OUTPUT
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		IF @ALL = 0
			SELECT 
				a.ID_TYPE, b.NAME
			FROM
				Claim.DocumentType a
				INNER JOIN Claim.Type b ON a.ID_TYPE = b.ID
			WHERE ID_DOCUMENT = @ID
			ORDER BY b.NAME
		ELSE
		BEGIN
			SELECT 
				CONVERT(BIT, CASE
					WHEN a.ID IS NULL THEN 0
					ELSE 1
				END) AS CHECKED,
				b.ID AS ID_TYPE, b.NAME
			FROM
				Claim.Type b
				LEFT OUTER JOIN Claim.DocumentType a ON a.ID_TYPE = b.ID AND ID_DOCUMENT = @ID
			WHERE (b.STATUS = 1 OR a.ID IS NOT NULL)
			ORDER BY b.NAME

			SELECT @RC = COUNT(*)
			FROM Claim.DocumentType
			WHERE ID_DOCUMENT = @ID
		END

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Claim].[CLAIM_TYPE_SELECT] TO rl_claim_r;
GO
