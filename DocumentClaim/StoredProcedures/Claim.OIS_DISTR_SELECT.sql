USE [DocumentClaim]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [Claim].[OIS_DISTR_SELECT]
	@ID	INT
WITH EXECUTE AS OWNER
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		EXEC Maintenance.START_PROC @@PROCID

		SELECT b.ID AS ID_SYSTEM, b.SHORT AS SYS_STR, c.ID AS ID_NET, c.SHORT AS NET_STR
		FROM
			[PC275-SQL\ALPHA].ClientDB.dbo.ClientDistrView a WITH(NOEXPAND)
			INNER JOIN Distr.System b ON a.SystemBaseName = b.REG
			CROSS APPLY
				(
					SELECT TOP 1 ID, SHORT
					FROM
						[PC275-SQL\ALPHA].ClientDB.Din.NetType
						INNER JOIN Distr.Net ON NET_COUNT = NT_NET AND TECH = NT_TECH
					WHERE NT_ID_MASTER = DistrTypeID
					ORDER BY NT_TECH, NT_NET
				) AS c
		WHERE a.ID_CLIENT = @ID AND DS_REG = 0
		ORDER BY b.ORD, c.SHORT

		EXEC Maintenance.FINISH_PROC @@PROCID
	END TRY
	BEGIN CATCH
		DECLARE	@SEV	INT
		DECLARE	@STATE	INT
		DECLARE	@NUM	INT
		DECLARE	@PROC	NVARCHAR(128)
		DECLARE	@MSG	NVARCHAR(2048)

		SELECT
			@SEV	=	ERROR_SEVERITY(),
			@STATE	=	ERROR_STATE(),
			@NUM	=	ERROR_NUMBER(),
			@PROC	=	ERROR_PROCEDURE(),
			@MSG	=	ERROR_MESSAGE()

		EXEC Maintenance.ERROR_RAISE @SEV, @STATE, @NUM, @PROC, @MSG
	END CATCH
END
GO
GRANT EXECUTE ON [Claim].[OIS_DISTR_SELECT] TO rl_claim_detail;
GO
